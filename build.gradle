buildscript {
  // Not sure how to move these to dependencies.gradle.
  // Someone with better Gradle skills, please help?
  ext.projectVersions = [
      minSdk: 23,
      compileSdk: 29,
      kotlin: '1.3.61',     // Keep in sync with dependencies.gradle
      sqlDelight: '1.2.1',  // keep in sync with dependencies.gradle.
  ]

  repositories {
    google()
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/badoo/maven" }
    maven { url "https://dl.bintray.com/robstoll/tutteli-jars" }
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:3.5.2'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$projectVersions.kotlin"
    classpath "com.squareup.sqldelight:gradle-plugin:$projectVersions.sqlDelight"
  }
}

plugins {
  id "org.jlleitschuh.gradle.ktlint" version "9.1.1" apply false
}

allprojects {
  repositories {
    google()
    mavenCentral()
    jcenter()
    maven { url 'https://dl.bintray.com/badoo/maven' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
  }
}

// TODO: can this be moved to a separate file?
subprojects {
  apply plugin: "org.jlleitschuh.gradle.ktlint"

  ktlint {
    version = "0.36.0"
    disabledRules = ["import-ordering", "chain-wrapping", "no-multi-spaces", "no-blank-line-before-rbrace"]
    filter {
      exclude { it.file.path.contains("build/") }
    }
  }
}

apply from: 'gradle/dependencies.gradle'
