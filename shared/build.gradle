apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'org.jetbrains.kotlin.native.cocoapods'
apply plugin: 'kotlin-android-extensions'
apply plugin: "com.squareup.sqldelight"

android {
  compileSdkVersion projectVersions.compileSdk
  defaultConfig {
    minSdkVersion projectVersions.minSdk
  }
}

sqldelight {
  ComposeDatabase {
    packageName = "me.saket.compose"
    schemaOutputDirectory = file("src/commonMain/db/databases")
  }
}

// Not sure, but probably used by CocoaPods.
version = 0.1

kotlin {
  android("android")
  // This is for iPhone emulator
  // Switch here to iosArm64 (or iosArm32) to build library for iPhone device
  iosX64("ios")

  sourceSets {
    commonMain {
      dependencies {
        implementation kotlin('stdlib-common')
        implementation deps.reaktive.core.common
        implementation deps.koin.core.common
        implementation deps.kNativeUuid
        implementation deps.klock.common
        implementation deps.sqlDelight.runtime.common
      }
    }
    commonTest {
      dependencies {
        implementation kotlin('test-common')
        implementation kotlin('test-annotations-common')
        implementation deps.reaktive.testing.common
        implementation deps.sqlDelight.driver.jdbcSqlite
      }
    }
    androidMain {
      dependencies {
        implementation kotlin('stdlib')
        implementation deps.timber
        implementation deps.rx2.core
        implementation deps.reaktive.interop.android
        implementation deps.sqlDelight.driver.android
      }
    }
    androidTest {
      dependencies {
        implementation kotlin('test')
        implementation kotlin('test-junit')
      }
    }
    iosMain {
      dependencies {
        implementation deps.sqlDelight.driver.iOS
      }
    }
    iosTest {}

    all {
      languageSettings {
        enableLanguageFeature('InlineClasses')
        useExperimentalAnnotation('kotlin.Experimental')
      }
    }
  }

  cocoapods {
    // Configure fields required by CocoaPods.
    summary = "Compose"
    homepage = "https://github.com/saket/compose"
  }
}

dependencies {}
