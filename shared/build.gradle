apply plugin: 'com.android.library'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'org.jetbrains.kotlin.native.cocoapods'

android {
  compileSdkVersion projectVersions.compileSdk
  defaultConfig {
    minSdkVersion projectVersions.minSdk
  }
}

// Not sure, but probably used by CocoaPods.
version = 0.1

kotlin {
  android("android")
  // This is for iPhone emulator
  // Switch here to iosArm64 (or iosArm32) to build library for iPhone device
  iosX64("ios")

  sourceSets {
    commonMain {
      dependencies {
        implementation kotlin('stdlib-common')
        implementation deps.reaktive.core.common

        // Bug workaround: IntelliJ seems to have issues with auto-complete
        // when multiple local multiplatform modules are present. Adding
        // multiple modules to androidApp/build.gradle
        api project(':wysiwyg')
      }
    }
    commonTest {
      dependencies {
        implementation kotlin('test-common')
        implementation kotlin('test-annotations-common')
        implementation deps.reaktive.testing.common
      }
    }
    androidMain {
      dependencies {
        implementation kotlin('stdlib')
        implementation deps.timber
        implementation deps.rx2.core
        implementation deps.reaktive.interop.android
      }
    }
    androidTest {
      dependencies {
        implementation kotlin('test')
        implementation kotlin('test-junit')
      }
    }
    iosMain {}
    iosTest {}

    all {
      languageSettings {
        enableLanguageFeature('InlineClasses')
      }
    }
  }

  cocoapods {
    // Configure fields required by CocoaPods.
    summary = "Compose"
    homepage = "https://github.com/saket/compose"
  }
}

dependencies {}
